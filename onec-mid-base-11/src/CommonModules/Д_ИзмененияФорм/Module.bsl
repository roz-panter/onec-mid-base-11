
 #Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма); 
		ДобавитьГруппуСкидка(Форма);
		ДобавитьПолеСогласованнаяСкидка(Форма);
		ДобавитьКомандуПересчитатьТаблицу(Форма);  	
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма); 
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);    
	КонецЕсли;
	
КонецПроцедуры   
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("ЭлементКонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	ПолеВвода.Заголовок = "Контактное лицо";   
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("ЭлементКонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
	 
	
КонецПроцедуры

Процедура ДобавитьГруппуСкидка(Форма)
	
	Группа = Форма.Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа; 
	Группа.Заголовок = "Группа скидка"; 
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
	
КонецПроцедуры 

     
Процедура ДобавитьПолеСогласованнаяСкидка(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("Д_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидка);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Д_СогласованнаяСкидка";
	ПолеВвода.УстановитьДействие("ПриИзменении", "Д_СогласованнаяСкидкаПриИзменении");
	
КонецПроцедуры 

Процедура ДобавитьКомандуПересчитатьТаблицу(Форма)
	
	Команда = Форма.Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";  
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	КнопкаФормы = Форма.Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу"; 
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	
КонецПроцедуры 


Процедура  ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("ЭлементКонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;  
	ПолеВвода.ПутьКДанным = "Объект.Д_КонтактноеЛицо";
      	  
КонецПроцедуры
#КонецОбласти
